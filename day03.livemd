# Day 3: Perfectly Spherical Houses in a Vacuum

```elixir
Mix.install([{:kino, "~> 0.8.0"}])
```

## Input

```elixir
input = Kino.Input.textarea("Please paste your input:")
```

```elixir
moves =
  input
  |> Kino.Input.read()
  |> String.codepoints()
```

## Part 1

Santa is delivering presents to an infinite two-dimensional grid of houses.

He begins by delivering a present to the house at his starting location, and then an elf at the North Pole calls him via radio and tells him where to move next. Moves are always exactly one house to the north (`^`), south (`v`), east (`>`), or west (`<`). After each move, he delivers another present to the house at his new location.

However, the elf back at the north pole has had a little too much eggnog, and so his directions are a little off, and Santa ends up visiting some houses more than once. How many houses receive **at least one present?**

For example:

* `>` delivers presents to `2` houses: one at the starting location, and one to the east.
* `^>v<` delivers presents to `4` houses in a square, including twice to the house at his starting/ending location.
* `^v^v^v^v^v` delivers a bunch of presents to some very lucky children at only `2` houses.

```elixir
moves
|> Enum.reduce({MapSet.new(), {0, 0}}, fn move, {visited, {x, y} = coord} ->
  visited = MapSet.put(visited, coord)

  coord =
    case move do
      "^" -> {x, y + 1}
      ">" -> {x + 1, y}
      "v" -> {x, y - 1}
      "<" -> {x - 1, y}
    end

  {visited, coord}
end)
|> elem(0)
|> MapSet.size()
```

## Part 2

The next year, to speed up the process, Santa creates a robot version of himself, **Robo-Santa**, to deliver presents with him.

Santa and Robo-Santa start at the same location (delivering two presents to the same starting house), then take turns moving based on instructions from the elf, who is eggnoggedly reading from the same script as the previous year.

This year, how many houses receive **at least one present**?

For example:

* `^v` delivers presents to `3` houses, because Santa goes north, and then Robo-Santa goes south.
* `^>v<` now delivers presents to `3` houses, and Santa and Robo-Santa end up back where they started.
* `^v^v^v^v^v` now delivers presents to `11` houses, with Santa going one direction and Robo-Santa going the other.

```elixir
defmodule Elf do
  def send({x, y}, move) do
    case move do
      "^" -> {x, y + 1}
      ">" -> {x + 1, y}
      "v" -> {x, y - 1}
      "<" -> {x - 1, y}
    end
  end
end

moves
|> Enum.with_index()
|> Enum.reduce({MapSet.new([{0, 0}]), {0, 0}, {0, 0}}, fn
  {move, i}, {visited, santa, robo} when rem(i, 2) == 0 ->
    santa = Elf.send(santa, move)
    visited = MapSet.put(visited, santa)
    {visited, santa, robo}

  {move, _}, {visited, santa, robo} ->
    robo = Elf.send(robo, move)
    visited = MapSet.put(visited, robo)
    {visited, santa, robo}
end)
|> elem(0)
|> MapSet.size()
```
