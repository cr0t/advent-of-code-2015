# Day 6: Probably a Fire Hazard

```elixir
Mix.install([{:kino, "~> 0.8.0"}])
```

## Input

```elixir
input = Kino.Input.textarea("Please paste your input:")
```

```elixir
defmodule Day6Shared do
  def parse(input) do
    input
    |> Kino.Input.read()
    |> String.split("\n")
    |> Enum.map(fn raw ->
      scan_ex = ~r/(turn on|toggle|turn off) (\d+),(\d+) through (\d+),(\d+)$/
      [_, cmd, f_x, f_y, t_x, t_y] = Regex.run(scan_ex, raw)

      f_x = String.to_integer(f_x)
      f_y = String.to_integer(f_y)
      t_x = String.to_integer(t_x)
      t_y = String.to_integer(t_y)

      {cmd, {f_x, f_y}, {t_x, t_y}}
    end)
  end
end

Day6Shared.parse(input)
```

## Part 1

Because your neighbors keep defeating you in the holiday house decorating contest year after year, you've decided to deploy one million lights in a 1000x1000 grid.

Furthermore, because you've been especially nice this year, Santa has mailed you instructions on how to display the ideal lighting configuration.

Lights in your grid are numbered from 0 to 999 in each direction; the lights at each corner are at `0,0`, `0,999`, `999,999`, and `999,0`. The instructions include whether to `turn on`, `turn off`, or `toggle` various inclusive ranges given as coordinate pairs. Each coordinate pair represents opposite corners of a rectangle, inclusive; a coordinate pair like `0,0 through 2,2` therefore refers to 9 lights in a 3x3 square. The lights all start turned off.

To defeat your neighbors this year, all you have to do is set up your lights by doing the instructions Santa sent you in order.

For example:

* `turn on 0,0 through 999,999` would turn on (or leave on) every light.
* `toggle 0,0 through 999,0` would toggle the first line of 1000 lights, turning off the ones that were on, and turning on the ones that were off.
* `turn off 499,499 through 500,500` would turn off (or leave off) the middle four lights.

After following the instructions, **how many lights are lit**?

```elixir
defmodule Day6Part1 do
  def process(commands) do
    Enum.reduce(commands, %{}, fn
      {"turn on", from, to}, lights ->
        list_lights(from, to)
        |> Enum.reduce(lights, &Map.put(&2, &1, :on))

      {"turn off", from, to}, lights ->
        list_lights(from, to)
        |> Enum.reduce(lights, &Map.put(&2, &1, :off))

      {"toggle", from, to}, lights ->
        list_lights(from, to)
        |> Enum.reduce(lights, fn coord, lights ->
          Map.update(lights, coord, :on, fn
            :off -> :on
            :on -> :off
          end)
        end)
    end)
  end

  def list_lights({f_x, f_y}, {t_x, t_y}),
    do: for(x <- f_x..t_x, y <- f_y..t_y, do: {x, y})
end

input
|> Day6Shared.parse()
|> Day6Part1.process()
|> Map.values()
|> Enum.count(&(&1 == :on))
```
